## ZAnc/Snakefile: pipeline to call ancestry outliers

workdir: path_hilo
# note: working directory is hilo/ and all file inputs/outputs below are relative to that working directory

## calc_K_matrix_zea: calculate K matrix for each ancestry
rule calc_K_matrix_zea:
    input:
        pop_anc = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pops.anc.RData",
        k_functions = "ZAnc/k_matrix.R"
    output:
        K_matrix = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.K.RData"
    params:
        p = "med2",
        K_admix = lambda wildcards: wildcards.K
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = lambda wildcards, attempt: attempt * 20,
        mem = lambda wildcards, attempt: attempt * 2
    script:
        "calc_K_matrix_zea.R"

## simulate_MVN: simulate ancestry data based on a multi-variate normal model of ancestry drift
rule simulate_MVN:
    input:
        K = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.K.RData",
        meta_pop = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pop.meta.RData"
    output:
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.RData",
        truncated_txt = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.truncated.stats.txt"
    params:
        p = "med2",
        n_sim = 100000,
        K_admix = lambda wildcards: wildcards.K
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = lambda wildcards, attempt: attempt * 4
    script:
        "simulate_MVN.R"

## fit_lm_elev: fits simple linear model lm(mexicana anc ~ elev)
rule fit_lm_elev:
    input:
        fdr_functions = "ZAnc/FDR.R",
        lm_functions = "ZAnc/lm_env_function.R",
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.RData",
        anc = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pops.anc.RData",
        meta_pop = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pop.meta.RData"
    output:
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fdr.RData",
        fit = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fit.RData",
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.sim.RData" # fits from simulated MVN data
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 4*60,
        mem = lambda wildcards, attempt: attempt * 4
    script:
        "fit_lm_elev.R"

## calc_fdr_mean_anc: calculates false discovery rates for mean ancestry based on MVN simulations
rule calc_fdr_mean_anc:
    input:
        fdr_functions = "ZAnc/FDR.R",
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.RData",
        anc = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pops.anc.RData"
    output:
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.meanAnc.fdr.RData"
    params:
        p = "med2",
        zea = lambda wildcards: wildcards.ZEA,
        K_admix = lambda wildcards: wildcards.K
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 4
    script:
        "calc_fdr_mean_anc.R"

## plot_mean_anc: plots mean ancestry across all samples and outlier regions
rule plot_mean_anc:
    input:
        anc = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pops.anc.RData",
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.meanAnc.fdr.RData",
        meta_pop = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pop.meta.RData",
        colors = "colors.R",
        sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.var.sites",
        genome = ref_chr,
        centromeres = "data/refMaize/centromere_positions_v4.txt"
    output:
        png = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_{ZEA}_mean_{ANCESTRY}_anc.png",
        rds = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_{ZEA}_mean_{ANCESTRY}_anc.plot.rds" # ggplot object
    params:
        p = "med2",
        zea = lambda wildcards: wildcards.ZEA,
        ancestry = lambda wildcards: wildcards.ANCESTRY
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 2
    script:
        "plot_mean_anc.R"

## plot_slope_elev: plots lm results, slope with elevation, with outlier regions
rule plot_slope_elev:
    input:
        meta_pop = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pop.meta.RData",
        colors = "colors.R",
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fdr.RData",
        fit = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fit.RData",
        sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.var.sites",
        genome = ref_chr,
        centromeres = "data/refMaize/centromere_positions_v4.txt"
    output:
        png = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_{ZEA}_slope_elev.png",
        rds = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_{ZEA}.lmElev.plot.rds" # ggplot object
    params:
        p = "med2",
        zea = lambda wildcards: wildcards.ZEA
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 2
    script:
        "plot_slope_elev.R"

## combine_genome_scan_plots: plots maize/mex mean ancestry and slope with elevation outliers plots across the genome in one figure
rule combine_genome_scan_plots:
    input:
        rds_maize_mean = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_maize_mean_maize_anc.plot.rds",
        rds_mexicana_mean = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_mexicana_mean_maize_anc.plot.rds",
        rds_maize_elev = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_maize.lmElev.plot.rds",
        rds_mexicana_elev = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_mexicana.lmElev.plot.rds",
        genome = "data/refMaize/Zea_mays.AFPv4.dna.chr.autosome.lengths",
        mhl1_QTL =  "data/known_QTL/chr9_bin4_mhl1_locus_v4.bed",
        inv =  "data/refMaize/inversions/knownInv_v4_coord.txt",
        genes = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/genes_mapped_to_outliers.txt"
    output:
        png = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_multi_maize_mexicana_genome_scan.png",
        png_lzw = "../hilo_manuscript/figures_main/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_multi_maize_mexicana_genome_scan.tif"
    params:
        p = "med2"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 2
    script:
        "combine_genome_scan_plots.R"

## combine_supp_genome_scan_plots: plots mex/parv mean ancestry supplementary outliers plots across the genome in one figure
rule combine_supp_genome_scan_plots:
    input:
        rds_maize_parv = "ZAnc/plots/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_maize_mean_parv_anc.plot.rds",
        rds_mexicana_parv = "ZAnc/plots/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_mexicana_mean_parv_anc.plot.rds",
        rds_maize_mexicana = "ZAnc/plots/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_maize_mean_mexicana_anc.plot.rds",
        rds_mexicana_mexicana = "ZAnc/plots/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_maize_mean_mexicana_anc.plot.rds",
        genome = "data/refMaize/Zea_mays.AFPv4.dna.chr.autosome.lengths"
    output:
        png = "ZAnc/plots/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_supp_maize_mexicana_genome_scan.png",
        png_lzw = "../hilo_manuscript/figures_supp/{PREFIX}_K3_Ne{Ne}_{YESNO}Boot_supp_maize_mexicana_genome_scan.tif"
    params:
        p = "med2"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 2
    script:
        "combine_supp_genome_scan_plots.R"


## plot_QQ: make QQ plots for mean ancestry and slope anc ~ elev (quantiles in MVN simulations vs data)
rule plotQQ:
    input: # all summarized simulation and observed results
        expand("local_ancestry/results/ancestry_hmm/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/anc/{zea}.pops.anc.RData", zea = zea),
        expand("ZAnc/results/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/{zea}.MVN.RData", zea = zea),
        expand("ZAnc/results/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/{zea}.lmElev.fit.RData", zea = zea),
        expand("ZAnc/results/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/{zea}.lmElev.sim.RData", zea = zea)
    output:
        png = "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_QQ.png",
        png_lzw = "../hilo_manuscript/figures_supp/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_QQ.tif"
    params:
        p = "med2",
        Ne = "{Ne}",
        yesno = "{YESNO}",
        PREFIX = "{PREFIX}",
        K = "{K}"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 2
    script:
        "plot_QQ.R"

## make_bed_slope_elev: makes bed files with outlier regions for ancestry slope with elevation
rule make_bed_slope_elev:
    input:
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fdr.RData",
        fit = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.fit.RData",
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.lmElev.sim.RData",
        bed = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.bed"
    output:
        pos_fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_lmElev_outliers.fdr05.bed",
        neg_fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_lmElev_outliers.fdr05.bed",
        pos_perc = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_lmElev_outliers.perc05.bed",
        neg_perc = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_lmElev_outliers.perc05.bed",
        pos_p = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_lmElev_outliers.p05.bed",
        neg_p = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_lmElev_outliers.p05.bed"
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    script:
        "make_bed_slope_elev.R"

## make_bed_anc_outliers: makes bed files with outlier regions for mean mexicana ancestry (pos is above mean, neg is below mean)
rule make_bed_anc_outliers:
    input:
        fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.meanAnc.fdr.RData",
        sim = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.RData",
        anc = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.{ANCESTRY}_anc.bed"
    output:
        pos_fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_{ANCESTRY}_anc_outliers.fdr05.bed",
        neg_fdr = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_{ANCESTRY}_anc_outliers.fdr05.bed",
        pos_perc = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_{ANCESTRY}_anc_outliers.perc05.bed",
        neg_perc = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_{ANCESTRY}_anc_outliers.perc05.bed",
        pos_p = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_pos_{ANCESTRY}_anc_outliers.p05.bed",
        neg_p = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_neg_{ANCESTRY}_anc_outliers.p05.bed"
    params:
        p = "med2",
        ANCESTRY = "{ANCESTRY}"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    script:
        "make_bed_anc_outliers.R"

## merge_bed_outliers: merges adjacent outliers into contiguous regions
rule merge_bed_outliers:
    input:
        "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.bed"
    output:
        "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.merged.bed"
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 2,
        mem = 2
    shell:
        "bedtools merge -i {input} > {output}"

## find_flowering_time_genes_v4: for all flowering time genes on v2 coordinates, finds match on v4 coordinates. makes a bed file.
rule find_flowering_time_genes_v4:
    input:
        Li = "data/flowering_time/v2_unique_gene_names_Li_et_al_2016.csv",
        Dong = "data/flowering_time/floweringTimeMaizeDong2012.csv",
        cross_ref = "data/refMaize/geneAnnotations/gene_model_xref_v4_from_gramene.txt"
    output:
        bed = "ZAnc/results/flowering_time_genes_v4.bed"
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    script:
        "flowering_time_genes.R"

## add_20kb_flowering_time_genes: adds 20kb upstream and downstream to all flowering time genes found on v4 chromosome coordinates
rule add_20kb_flowering_time_genes:
    input:
        bed = "ZAnc/results/flowering_time_genes_v4.bed",
        genome = ref_chr
    output:
        bed = "ZAnc/results/flowering_time_genes_v4.plus20kb.bed"
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    shell: # also gets rid of header
        "tail -n +2 {input.bed} | bedtools slop -i - -g {input.genome} -b 20000 > {output.bed}"

## calc_overlap: find overlap gene set & outliers. Also count number of outliers in original gene set (line 1) and 100 randomly shuffled positions from that gene set (lines 2-101)
rule calc_overlap:
    input:
        genes = "ZAnc/results/flowering_time_genes_v4.plus20kb.bed",
        outliers = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.merged.bed",
        genome = ref_chr
    output:
        bed = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/flowering_time_genes_v4.plus20kb.{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.bed",
        shuffle = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/flowering_time_genes_v4.plus20kb.{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.counts"
    params:
        p = "med2"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    shell:
        """
        bedtools intersect -a {input.genes} -b {input.outliers} -u > {output.bed}
        wc -l {output.bed} > {output.shuffle}
        for i in {{1..1000}}
            do bedtools shuffle -i {input.genes} -g {input.genome} | \\
            bedtools intersect -a - -b {input.outliers} -u | wc -l >> {output.shuffle}
        done
        """

## summarise_overlap_flowering_time: summarises number of outliers in original gene set (line 1) and 1000 randomly shuffled positions from that gene set (lines 2-1001)
rule summarise_overlap_flowering_time:
    input:
        expand("ZAnc/results/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/flowering_time_genes_v4.plus20kb.{ZEA}_{POSNEG}_{STAT}_outliers.{SIG}.counts",
        ZEA = zea, POSNEG = ["pos", "neg"], STAT = ["lmElev", "mexicana_anc"], SIG = ["fdr05", "perc05", "p05"])
    output:
        txt = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/flowering_time_genes_v4.plus20kb.overlap.summary_overlap_outliers.txt"
    params:
        p = "med2",
        Ne = lambda wildcards: wildcards.Ne,
        yesno = lambda wildcards: wildcards.YESNO,
        prefix = "{PREFIX}",
        K = "{K}"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 15,
        mem = 2
    script:
        "summarise_overlap_flowering_time.R"

## test_key_genes: checks short list of key genes against outlier regions
rule test_key_genes:
    input:
        anc_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pops.anc.RData",
        anc_fdr_maize = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/maize.meanAnc.fdr.RData",
        lmElev_maize = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/maize.lmElev.fit.RData",
        lmElev_fdr_maize = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/maize.lmElev.fdr.RData",
        meta_pop_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pop.meta.RData",
        anc_mexicana = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pops.anc.RData",
        anc_fdr_mexicana = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/mexicana.meanAnc.fdr.RData",
        lmElev_mexicana = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/mexicana.lmElev.fit.RData",
        lmElev_fdr_mexicana = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/mexicana.lmElev.fdr.RData",
        meta_pop_mexicana = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pop.meta.RData",
        sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.var.sites",
        genome = ref_chr,
        genes_list = "data/key_genes.csv",
        bed_sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.bed"
    output:
        txt = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/genes_mapped_to_outliers.txt",
        tbl = "ZAnc/tables/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_genes_mapped_to_outliers.tex",
        tbl2 = "../hilo_manuscript/tables/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_genes_mapped_to_outliers.tex"
    params:
        p = "med2",
        Ne = lambda wildcards: wildcards.Ne,
        yesno = lambda wildcards: wildcards.YESNO,
        prefix = "{PREFIX}",
        K = "{K}"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 8
    script:
        "test_key_genes.R"

## fit_zAnc: zTz, lm(zAnc ~ zElev) and lm(zAnc ~ intercept) transforms ancestry using cholesky rotation, then fits association with transformed variables, or null model
#rule fit_zAnc:
#    input:
#        zAnc_functions = "ZAnc/other_functions.R",
#        sim = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.MVN.RData",
#        anc = "local_ancestry/results/ancestry_hmm/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pops.anc.RData",
#        meta_pop = "local_ancestry/results/ancestry_hmm/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/anc/{ZEA}.pop.meta.RData",
#        K = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.K.RData"
#    output:
#        sim = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.zAnc.sim.RData",
#        fit = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.zAnc.fit.RData"
#    params:
#        p = "med2"
#    shadow:
#        "minimal"
#    conda:
#        "../envs/environment.yaml"
#    threads:
#        1
#    resources:
#        time_min = 3*60,
#        mem = 8
#    script:
#        "fit_zAnc_models.R"

## fdr_zAnc: calculates false discovery rates for zAnc models based on comparing enrichment for outliers in the data to the simulated MVN null model
#rule fdr_zAnc:
#    input:
#        sim = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.zAnc.sim.RData",
#        fit = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.zAnc.fit.RData",
#        fdr_functions = "ZAnc/FDR.R"
#    output:
#        fdr = "ZAnc/results/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/{ZEA}.zAnc.fdr.RData"
#    params:
#        p = "med2"
#    shadow:
#        "minimal"
#    conda:
#        "../envs/environment.yaml"
#    threads:
#        1
#    resources:
#        time_min = 6*60,
#        mem = 8
#    script:
#        "fdr_zAnc_models.R"

## plot_shared_peaks: plots shared outlier peaks for high introgression
rule plot_shared_peaks:
    input:
        sim_maize = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/maize.MVN.RData",
        sim_mex = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/mexicana.MVN.RData",
        anc_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pops.anc.RData",
        anc_mex = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pops.anc.RData",
        meta_pop_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pop.meta.RData",
        meta_pop_mex = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pop.meta.RData",
        meta = "samples/{PREFIX}_meta.RData",
        colors = "colors.R",
        sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.var.sites",
        inv = "data/refMaize/inversions/knownInv_v4_coord.txt",
        genome = "data/refMaize/Zea_mays.AFPv4.dna.chr.autosome.lengths"
    output:
        "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_network_peak_sharing_data_only.png",
        "../hilo_manuscript/figures_main/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_network_peak_sharing_data_only.tif",

        "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_combmatrix_peak_sharing_maize.png",
        "../hilo_manuscript/figures_supp/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_combmatrix_peak_sharing_maize.tif",

        "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_combmatrix_peak_sharing_mexicana.png",
        "../hilo_manuscript/figures_supp/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_combmatrix_peak_sharing_mexicana.tif",

        "ZAnc/plots/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_shared_peaks_on_genome.png",

        expand("ZAnc/plots/{{PREFIX}}_K{{K}}_Ne{{Ne}}_{{YESNO}}Boot_{ZEA}_shared_outliers_chr_{i}.png", i = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10], ZEA = zea),
        "../hilo_manuscript/figures_main/{PREFIX}_K{K}_Ne{Ne}_{YESNO}Boot_maize_shared_outliers_chr_4.tif",
        expand("../hilo_manuscript/figures_supp/{{PREFIX}}_K{{K}}_Ne{{Ne}}_{{YESNO}}Boot_{ZEA}_shared_outliers_chr_{i}.tif", i = [1, 2, 3, 5, 6, 7, 8, 9, 10], ZEA = "maize"),
        expand("../hilo_manuscript/figures_supp/{{PREFIX}}_K{{K}}_Ne{{Ne}}_{{YESNO}}Boot_{ZEA}_shared_outliers_chr_{i}.tif", i = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10], ZEA = "mexicana")
    params:
        p = "med2",
        Ne = lambda wildcards: wildcards.Ne,
        YESNO = lambda wildcards: wildcards.YESNO,
        PREFIX = "{PREFIX}",
        K = "{K}"
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 30,
        mem = 16
    script:
        "plot_shared_peaks.R"

## make_rf_outliers_by_pop: make regions files (-rf for angsd) and bed files for high introgression outlier regions in a focal population that are shared by no other populations (1pop) or shared by at least 3 other populations (4pop)
rule make_rf_outliers_by_pop:
    input:
        anc_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pops.anc.RData",
        anc_mexicana = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pops.anc.RData",
        meta_maize = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/maize.pop.meta.RData",
        meta_mexicana = "local_ancestry/results/ancestry_hmm/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot/anc/mexicana.pop.meta.RData",
        meta = "samples/{PREFIX}_meta.RData",
        bed_sites = "local_ancestry/results/thinnedSNPs/{PREFIX}/K{K}/whole_genome.bed",
        genome = ref_chr
    output:
        expand("ZAnc/results/{{PREFIX}}/K{{K}}/Ne{{Ne}}_{{YESNO}}Boot/{{POP}}.{n}pop.outliers.{SUFFIX}", n = [1, 4], SUFFIX = ["regions", "bed"])
    params:
        p = "med2",
        dir_out = "ZAnc/results/{PREFIX}/K{K}/Ne{Ne}_{YESNO}Boot",
        focal_pop = lambda wildcards: wildcards.POP
    conda:
        "../envs/environment.yaml"
    threads:
        1
    resources:
        time_min = 15,
        mem = 4
    script:
        "make_rf_outliers_by_pop.R"
