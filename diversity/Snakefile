## diversity/Snakefile: pipeline to get pi and Fst estimates

# note: working directory is hilo/ and all file inputs/outputs below are relative to that working directory

## make_fasta: makes ancestral fasta from tripsacum using concensus (most frequent) base
rule make_fasta:
    input:
        bam = "filtered_bams/results/SRR7758238/TRIP.sort.dedup.baq.bam",
        bai = "filtered_bams/results/SRR7758238/TRIP.sort.dedup.baq.bam.bai"
    output:
        fasta = trip_anc
    params:
        p = "med2",
        out_prefix = "filtered_bams/results/SRR7758238/TRIP"
    shadow:
        "minimal"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 4*60,
        mem = lambda wildcards, attempt: attempt * 8 + 16
    shell:
        "angsd -out {params.out_prefix} "
        "-i {input.bam} "
        "-doFasta 2 "
        "-remove_bads 1 -minMapQ 30 -minQ 20 "
        "-doCounts 1 "

## estimate_saf_pop: estimate site allele frequency likelihoods using ANGSD for a population
rule estimate_saf_pop:
    input:
        anc = "filtered_bams/results/SRR7758238/TRIP.fa.gz",
        bam_list = "local_ancestry/results/ancestry_hmm/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/bams/{POP}_bams.list",
        complete = "local_ancestry/results/ancestry_hmm/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/bams/{POP}.completed" # has bams
    output:
        saf_idx = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.saf.idx", # ALL or HOMOZYG/maize. maybe start with HOMOZYG/mexicana
        saf_gz = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.saf.gz",
        saf_pos = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.saf.pos.gz"
    params:
        p = "med2",
        out_prefix = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}"
    conda:
        "../envs/environment.yaml"
    threads:
        2
    resources:
        time_min = 24*60,
        mem = lambda wildcards, attempt: attempt * 16 + 32
    shell:
        "angsd -out {params.out_prefix} "
        "-doSaf 1 -anc {input.anc} "
        "-GL 1 "
        "-P {threads} "
        "-bam {input.bam_list} "
        "-remove_bads 1 -minMapQ 30 -minQ 20 "

## estimate_sfs: estimates site frequency spectrum (sfs) by maximum likelihood in ANGSD
rule estimate_sfs:
    input:
        saf = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.saf.idx"
    output:
        sfs = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.sfs"
    params:
        p = "med2"
    conda:
        "../envs/environment.yaml"
    threads:
        6
    resources:
        time_min = 24*60,
        mem = lambda wildcards, attempt: attempt * 8 + 32 # NEEDS LOTS OF MEM!! If unreasonable to load whole saf.gz into memory will need to set -nSites
    shell:
        "realSFS {input.saf} -P {threads} > {output.sfs}"

## calc_thetas: calculates thetas for each site from saf and sfs in ANGSD
rule calc_thetas:
    input:
        saf = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.saf.idx",
        sfs = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.sfs"
    output:
        gz = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.thetas.gz",
        idx = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.thetas.idx"
    params:
        p = "med2",
        out_prefix = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 12*60,
        mem = lambda wildcards, attempt: attempt * 8
    shell:
        "realSFS saf2theta {input.saf} -sfs {input.sfs} -outname {params.out_prefix}"

## calc_pi_by_windows: calculates pi for windows of set size
rule calc_pi_by_windows:
    input:
        idx = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.thetas.idx"
    output:
        thetas = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.pi.windows.{WIN}.{STEP}.pestPG"
    params:
        p = "bigmemm",
        WIN = lambda wildcards: wildcards.WIN,
        STEP = lambda wildcards: wildcards.STEP,
        out_prefix = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.pi.windows.{WIN}.{STEP}"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 4*60,
        mem = lambda wildcards, attempt: attempt * 8
    shell:
        "thetaStat do_stat {input.idx} -win {WIN} -step {STEP} -outnames {params.out_prefix}"

## calc_pi_all_chr: calculates pi for each chromosome in the genome (for background diversity rates)
rule calc_pi_all_chr:
    input:
        idx = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.thetas.idx"
    output:
        thetas = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.pi.allChr.pestPG"
    params:
        p = "bigmemm",
        out_prefix = "diversity/results/pi/" + prefix_all + "/Ne{Ne}_{YESNO}Boot/HOMOZYG/{ZEA}/{POP}.pi.allChr"
    conda:
        "../envs/environment.yaml"
    resources:
        time_min = 4*60,
        mem = lambda wildcards, attempt: attempt * 8
    shell:
        "thetaStat do_stat {input.idx} -outnames {params.out_prefix}"
